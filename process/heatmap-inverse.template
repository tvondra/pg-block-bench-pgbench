set terminal postscript eps size 6,6 enhanced color font 'Helvetica,12'
set output 'heatmap-DATASET.eps'

set palette defined (0 "web-green", 0.5 "white", 1 "red")

set key off
set xlabel "data block"
set ylabel "WAL block"

# set labels for x/y axis to block sizes"
XTICS="1 2 4 8 16 32"
YTICS="1 2 4 8 16 32 64"

set for [i=1:words(XTICS)] xtics ( word(XTICS,i) i-1 )
set for [i=1:words(YTICS)] ytics ( word(YTICS,i) i-1 )

# don't show color scale next to the heatmap
unset colorbox

set multiplot layout 3, 2 rowsfirst


set title "machine: MACHINE scale: SCALE_A, mode: read-only"

plot "SCALE_A-ro-DATASET.data" matrix using 1:2:3 with image, \
     "SCALE_A-ro-DATASET.data" matrix using 1:2:(sprintf("%g",$3)) with labels

set title "machine: MACHINE scale: SCALE_A, mode: read-write"

plot "SCALE_A-rw-DATASET.data" matrix using 1:2:3 with image, \
     "SCALE_A-rw-DATASET.data" matrix using 1:2:(sprintf("%g",$3)) with labels


set title "machine: MACHINE scale: SCALE_B, mode: read-only"

plot "SCALE_B-ro-DATASET.data" matrix using 1:2:3 with image, \
     "SCALE_B-ro-DATASET.data" matrix using 1:2:(sprintf("%g",$3)) with labels

set title "machine: MACHINE scale: SCALE_B, mode: read-write"

plot "SCALE_B-rw-DATASET.data" matrix using 1:2:3 with image, \
     "SCALE_B-rw-DATASET.data" matrix using 1:2:(sprintf("%g",$3)) with labels


set title "machine: MACHINE scale: SCALE_C, mode: read-only"

plot "SCALE_C-ro-DATASET.data" matrix using 1:2:3 with image, \
     "SCALE_C-ro-DATASET.data" matrix using 1:2:(sprintf("%g",$3)) with labels

set title "machine: MACHINE scale: SCALE_C, mode: read-write"

plot "SCALE_C-rw-DATASET.data" matrix using 1:2:3 with image, \
     "SCALE_C-rw-DATASET.data" matrix using 1:2:(sprintf("%g",$3)) with labels
